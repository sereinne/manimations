# default path where `manim` generates its artifacts
path := "output/videos/main/1080p30/"

alias rc := run-clean

combinevid:
   # get all complete video files and sort them based on the oldest to newest video files
   # the output of the command above is in one line, make them intro separate lines
   # for each new line (that is the path to the video files), format it into 'file ./path/to/video/file.mp4' redirect echo's output, intro a file called `target_files`
   fd . --max-depth 1 -e mp4 --full-path {{path}} -X ls -tr \
   | tr ' ' '\n' \
   | xargs -I {} echo "file '{}'" > target_files
   # make `ffmpeg` concatenate those video files based on the ordering inside `target_files`
   ffmpeg -f concat -i target_files -c copy final.mp4
   # cleanup
   rip target_files

makevid:
   # make `manim` generate all active scenes 
   manim main.py $(rg --no-filename --no-line-number --only-matching "^class \w+" main.py | awk '{print $2}' | xargs -I {} echo "{}" | tr '\n' ' ')

# before `run-clean` is getting run, `just` will run `makevid` and `combinevid` command sequentially
run-clean: makevid combinevid
   # run the combined video
   mpv final.mp4
   # remove the combined video
   rip final.mp4
   # remove the output directory
   rip output/
   # remove from disk
   yes | rip -d
